	Like most objects in a \IXpov{} \IXarg{scene},
	the objects exported by \IXpkg{} need to be
	\IXarg{\emph{transformed}} in various ways to be
	placed in the scene with a particular size and
	orientation. The \IXarg{\emph{translate}},
	\IXarg{\emph{scale}}, and
	\IXarg{\emph{rotate}} keywords are used to
	accomplish this. Also, a collection of transformations
	may be placed in a \IXarg{\emph{transform}} object.
	This section will suggest some useful initial transforms
	of prisms and lathes exported
	by \IXpkg{}.
	
	It was mentioned in \nameref{sec:creat_editing}
	that the \IXarg{coordinates} of the \IXpkg{}
	\IXarg{drawing area}, or ``\IXarg{canvas},''
	are not similar to the \IXpov{}
	\IXarg{\emph{coordinate system}}. In fact, the \IXarg{SDL}
	allows the \IXpov{} \IXarg{coordinate system}
	to be modified, and so assumptions made by a program
	such as \IXpkg{} might not be valid. It is better that
	the user understand \IXpov{} \IXarg{transforms} and
	place the objects logically. \IXpkgu{} declares a number
	of necessary values describing the (un-scaled) extremes
	and positions of objects, and using these declarations,
	the typical transforms are routine.


		\subsection{Example of Exported SDL}%WX%4||||
		\label{ssec:ex_sdl_exported}
		\begin{povsdl}
			{\small
			\begin{verbatim}
			// POV include file
			// Generated by epspline
			
			
			#ifndef ( INCLUDED_foo_inc )
			#declare INCLUDED_foo_inc = 1;
			
			#declare Foo_width = 300.000000;
			#declare Foo_height = 99.379610;
			#declare Foo_left = 100.000000;
			#declare Foo_right = 400.000000;
			#declare Foo_top = 100.000000;
			#declare Foo_bottom = 199.379610;
			#declare Foo_max_extent = max(Foo_width, Foo_height);
			
			#declare Foo_NORMAL_TRANSFORM = transform {
			  translate <-Foo_left - Foo_width / 2, 0, -Foo_top - Foo_height / 2>
			  scale <1 / Foo_max_extent, 1, 1 / Foo_max_extent>
			  scale <1, 1, -1>
			}
			
			#declare Foo =
			prism { bezier_spline linear_sweep -1.000000, 1.000000
			  , 8
			  , <100.000000, 100.000000>
			  , <186.000000, 162.644928>
			  , <315.000000, 160.717391>
			  , <400.000000, 100.000000>
			  , <400.000000, 100.000000>
			  , <316.000000, 232.036232>
			  , <186.000000, 233.000000>
			  , <100.000000, 100.000000>
			#ifdef ( Foo_USE_NORMAL_TRANSFORM )
			  transform { Foo_NORMAL_TRANSFORM }
			#end
			}
			
			#declare I_foo_inc_width = 300.000000;
			#declare I_foo_inc_height = 99.379610;
			#declare I_foo_inc_left = 100.000000;
			#declare I_foo_inc_right = 400.000000;
			#declare I_foo_inc_top = 100.000000;
			#declare I_foo_inc_bottom = 199.379610;
			
			#end // #ifdef ( INCLUDED_foo_inc )
			\end{verbatim}
			}%small
		\caption{One spline object named ``Foo'' exported as
			\emph{scene description language}.}
		\label{pvlst:sdl_one_obj}
		\end{povsdl}
	
		An example of the \IXarg{SDL} exported by \IXpkg{}
		is shown in listing~\ref{pvlst:sdl_one_obj}.
		The name of the object is ``Foo'', and the file it was
		exported to is named ``foo.inc''
		This ``include file'' begins (after an initial
		comment) with a guard against re-inclusion by testing and
		declaring
		``\texttt{INCLUDED\_foo\_inc}.''
		The guard name will
		integrate the file name, so it will probably
		be unique. If the file is
		included a second time the \texttt{\#ifndef} test will fail
		and everything will be ignored up to the matching
		\texttt{\#end} near the end of the file. This common guard
		allows one include file to be used by several other files
		without conflict.
		
		Following the guard, constants are declared for the
		dimensions and position of the the object.
		These values are collected
		by \IXpkg{} as the curve is calculated
		(i.e., \IXarg{interpolated}) for display,
		and are limited in accuracy by the resolution of the
		\IXarg{interpolation}, but they should be precise enough
		for any scene.
		
		Appearing next, just before the \IXspline{} object
		declaration, is the declaration of an optional transform,
		named ``\texttt{Foo\_NORMAL\_TRANSFORM}.''
		This is only applied if
		``\texttt{Foo\_USE\_NORMAL\_TRANSFORM}''
		is declared before the
		file is included. If used, the transform scales the object
		(not including the \emph{\IXarg{sweep}} of a \IXprism{})
		to one unit on the axis with the greater extent. It also
		centers the object at the origin, and inverts the object
		along the $z$-axis for the \IXprism{},
		or the $y$-axis for the \IXlathe{}
		(because of the difference in view
		between \IXpov{} and \IXpkg{}).
		
		Next, the object is declared. If there was more than
		one object in listing~\ref{pvlst:sdl_one_obj},
		then the sequence of constants-transform--object would
		be repeated for each.
		
		Finally, constants are declared that are similar to those
		declared for objects
		but which pertain to all the objects in the file.
		As listing~\ref{pvlst:sdl_one_obj} shows only one object,
		these happen to be the same as the constants for
		``\texttt{Foo},'' but if a file is composed of several
		objects meant to keep their relative positions and sizes,
		then these final constants are useful.


		\subsection{Example with a Lathe and Prism}%WX%4||||
		\label{ssec:ex_lath_and_prism}
		Consider a scene with two objects, a \IXlathe{} and a
		\IXprism{}, that should be rendered together. The \IXlathe{}
		is named ``Cup,'' and the \IXprism{} is named ``Handle.''
		Drawn quickly in \IXpkg{}, they might look like
		figure~\ref{fig:cup_doodle}.
		Hopefully, it is obvious which objects are assigned
		the names. Since lathes are rotated around
		the $y$-axis, ``Cup'' is the profile of half of a cup.
		Also, the left-most points of ``Cup'' are at the
		horizontal point zero. If those points had a positive
		offset, then the cup would have a hole in the bottom.
		If they had a negative offset, \IXpov{} would reject
		the object and exit with an error message: lathes may
		not have negative values on the rotated axis.

		\begin{figure}[htbp]
		\centering
		\includegraphics[width=\linewidth]{\ImgCupDoodle}
		\caption{A simple cup on the \dtypkg{} canvas.}
		\label{fig:cup_doodle}
		\end{figure}

		After these objects
		are exported to an include file, ``cup.inc,''
		a main scene file is necessary to render the cup
		with \IXpov{}. A minimal scene file, ``cup.pov,'' is shown in
		listing~\ref{pvlst:sdl_coffee_cup_pov}.
		The \IXpov{}
		\href{http://www.povray.org/documentation/}{documentation}
		will explain the \IXarg{language} (\IXarg{SDL}).
		
		\begin{povsdl}
			{\small
			\begin{verbatim}
			#version 3.5;
			#include "colors.inc"
			
			light_source { <20, 20, -20> color White }
			
			camera {
			    location <0, 0.75, -4>
			    look_at <0, 0.25, 0>
			    right x * image_width / image_height
			    angle 45
			}
			
			#declare CupTexture = texture { pigment { color LightBlue } }
			#declare BoxTexture = texture { pigment { color DarkGreen } }
			
			#include "cup.inc"
			
			#declare CoffeeCupRaw = union {
			    object { Handle
			        rotate x * -90
			    }
			    object { Cup }
			}
			
			#declare CoffeeCup = object {
			    CoffeeCupRaw
			    translate <0, -Cup_top - Cup_height / 2, 0>
			    scale 1 / Cup_height
			    scale <1, -1, 1>
			    texture { CupTexture }
			}
			
			object { CoffeeCup translate <0, 0.5, 0> }
			// somewhere to put the cup down:
			box { <-1, -0.25, -0.75> <1, 0, 0.75>
			    texture { BoxTexture }
			}
			\end{verbatim}
			}%small
		\caption{\dtypov{} SDL for a simple coffee cup scene.}
		\label{pvlst:sdl_coffee_cup_pov}
		\end{povsdl}

		Near the middle of listing~\ref{pvlst:sdl_coffee_cup_pov}
		the file
		``cup.inc'' is included. Next, the the two objects
		``Handle'' and ``Cup'' are bound together in a
		\IXnewterm{union} named ``CoffeeCupRaw,'' which has
		been called `Raw' because it is
		not yet an object ready to be presented.
		The first important transform is stated in the union:
		the handle is rotated by -90 degrees. Recall from
		\nameref{sec:creat_editing} that in the
		2D view of outlines in \IXpkg{} the \IXprism{} is seen
		parallel to the $y$-axis, and the \IXlathe{} is seen
		perpendicular to the $y$-axis. Therefore, rotating
		the handle brings it to the same line-of--sight as
		the cup.
		
		The union CoffeeCupRaw has put the the handle in juxtaposition
		to the cup, but the whole is still at a size and offset
		according to the units used in the \IXpkg{} interface.
		In the declaration of ``CoffeeCup'' the object is
		is centered (centering is the author's preference) and
		scaled to one unit on the axis which makes the most sense
		for this scene, the $y$-axis. In
		\nameref{ssec:ex_sdl_exported}
		the constants declared for an object were introduced.
		Some of these constants are used in transforming
		the coffee cup. The \emph{\IXarg{translate}}
		vector has zero at $x$ and $z$, but at $y$ the
		`top' offset is removed and half the raw height
		is subtracted. This $y$ translation is negative
		because the view of lathes in \IXpkg{} has $0$
		at top and increases downward. The next statement,
		a \emph{\IXarg{scale}} transform, reduces the height
		to one unit, and next another \emph{scale}
		inverts the cup along the $y$-axis for the same
		reason given for negative \IXarg{translation}.
		
		Finally, the cup is placed in the scene by adding
		it in an ``\IXarg{object}'' statement,
		without ``\verb!#declare!,'' and shifting it again
		along $y$ so that the bottom of the cup
		is at $0$.
		A box is placed so that the cup rests on it.
		The result is shown in
		figure~\ref{fig:cup_doodle_render}.

		\begin{figure}[htbp]
		\centering
		\includegraphics[width=0.75\linewidth]{\ImgCupDoodleRender}
		\caption{The simple cup rendered.}
		\label{fig:cup_doodle_render}
		\end{figure}

